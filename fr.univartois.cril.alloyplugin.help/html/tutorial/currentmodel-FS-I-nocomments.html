<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Alloy Tutorial Model</title>
    <link   rel=stylesheet   href="style-currentmodel.css"   type="text/css">
  </head>

  <body>
    <h2>File System Model (I)</h2>

<p> <a href="currentmodel-FS-I.html" target="currentmodel"> view
    uncompressed version</a>

<b><pre>
module models/examples/tutorial/filesystem

sig FSObject {parent: lone Dir}
sig Dir extends FSObject {contents: set FSObject}
sig File extends FSObject {}
one sig Root extends Dir {}{no parent}

fact defineContents {all d: Dir, o: d.contents | o.parent = d}
fact fileDirPartition {File + Dir = FSObject}
fact fileSystemConnected {FSObject in Root.*contents}

assert acyclic {no d: Dir | d in d.^contents}
check acyclic for 5

assert oneRoot {one d: Dir | no d.parent}
check oneRoot for 5

assert oneLocation {all o: FSObject | sole d: Dir | o in d.contents}
check oneLocation for 5
</pre></b>

<hr WIDTH="100%">
<p> The "main tutorial text" frame will provide you with links to go
    to the next version of the model, when the time is right.  If you
    wish to browse the different versions of the model on your own,
    you may do so with the links below:
<p> (there are no previous versions of this model)
<p> <a href="currentmodel-FS-II.html">next model</a> file system
example version II.

<hr WIDTH="100%">
<p> <a href="alloy-tutorial.html" target="_top">restart </a> the
tutorial from the beginning.

  </body>
</html>
