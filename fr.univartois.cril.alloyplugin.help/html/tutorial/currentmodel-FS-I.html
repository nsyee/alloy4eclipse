<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Alloy Tutorial Model</title>
    <link   rel=stylesheet   href="style-currentmodel.css"   type="text/css">
  </head>

  <body>
    <h2>File System Model (I)</h2>

<p> <a href="currentmodel-FS-I-nocomments.html" target="currentmodel">
    view compressed version</a>

<pre>
<b>module models/examples/tutorial/filesystem</b>

<i>// A file system object in the file system</i>
<b>sig FSObject {
  parent: lone Dir
}</b>

<i>// A directory in the file system</i>
<b>sig Dir extends FSObject {
  contents: set FSObject
}</b>

<i>// A directory is the parent of its contents</i>
<b>fact defineContents {
  all d: Dir, o: d.contents | o.parent = d
}</b>

<i>// A file in the file system</i>
<b>sig File extends FSObject {}</b>

<i>// All file system objects are either files or directories</i>
<b>fact fileDirPartition {
  File + Dir = FSObject
}</b>

<i>// There exists a root</i>
<b>one sig Root extends Dir {
}{
  no parent
}</b>

<i>// File system is connected</i>
<b>fact fileSystemConnected {
  FSObject in Root.*contents
}</b>

<i>// The contents path is acyclic</i>
<b>assert acyclic {
  no d: Dir | d in d.^contents
}</b>

<b>check acyclic for 5</b>

<i>// File system has one root</i>
<b>assert oneRoot {
  one d: Dir | no d.parent
}</b>

<b>check oneRoot for 5</b>

<i>// Every fsobject is in at most one directory</i>
<b>assert oneLocation {
  all o: FSObject | lone d: Dir | o in d.contents
}</b>

<b>check oneLocation for 5</b>
</pre>

<hr WIDTH="100%">
<p> The "main tutorial text" frame will provide you with links to go
    to the next version of the model, when the time is right.  If you
    wish to browse the different versions of the model on your own,
    you may do so with the links below:
<p> (there are no previous versions of this model)
<p> <a href="currentmodel-FS-II.html">next model</a> file system
example version II.

<hr WIDTH="100%">
<p> <a href="alloy-tutorial.html" target="_top">restart </a> the
tutorial from the beginning.

  </body>
</html>
