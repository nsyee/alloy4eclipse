<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Alloy Tutorial Model</title>
    <link   rel=stylesheet   href="style-currentmodel.css"   type="text/css">
  </head>

  <body>
    <h2>Ord module</h2>

<p> <a href="currentmodel-Ord.html" target="currentmodel"> view
    comments</a>

<pre><b>
module util/ordering[elem]

one sig Ord {
   first_, last_: elem,
   next_, prev_: elem -> lone elem
}{
  prev_ = ~next_
  one first_
  one last_
  no first_.prev_
  no last_.next_
  (
   (one elem && no elem.prev_ && no elem.next_) ||
    all e: elem | {
      (e = first_ || one e.prev_)
      (e = last_ || one e.next_)
      (e !in e.^next_)
    }
  )
  elem in first_.*next_
}

fun first (): elem { Ord.first_ }
fun last  (): elem { Ord.last_ }

fun prev (e: elem): lone elem { e.(Ord.prev_) }
fun next (e: elem): lone elem { e.(Ord.next_) }

fun prevs (e: elem): set elem { e.^(Ord.prev_) }
fun nexts (e: elem): set elem { e.^(Ord.next_) }

pred lt (e1, e2: elem) { e1 in prevs (e2) }
pred gt (e1, e2: elem) { e1 in nexts (e2) }
pred lte (e1, e2: elem) { e1=e2 || lt (e1,e2) }
pred gte (e1, e2: elem) { e1=e2 || gt (e1,e2) }

fun larger (e1, e2: elem): elem { if lt (e1,e2) then e2 else e1 }
fun smaller (e1, e2: elem): elem { if lt (e1,e2) then e1 else e2 }

fun max (es: set elem): lone elem { es - es.^(Ord.prev_) }
fun min (es: set elem): lone elem { es - es.^(Ord.next_) }
</b></pre>

<hr WIDTH="100%">

<p> <a href="currentmodel-Ord.html" target="currentmodel"> view
    comments</a>

<hr WIDTH="100%">

<p> <a href="alloy-tutorial.html" target="_top">restart </a> the
tutorial from the beginning.

  </body>
</html>
