<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Alloy Tutorial Model</title>
    <link   rel=stylesheet   href="style-currentmodel.css"   type="text/css">
  </head>

  <body>
    <h2>River Crossing Model</h2>

<pre>
<i>/*
 * The classic river crossing puzzle. A farmer is carrying a fox, a
 * chicken, and a sack of grain. He must cross a river using a boat
 * that can only hold the farmer and at most one other thing. If the
 * farmer leaves the fox alone with the chicken, the fox will eat the
 * chicken; and if he leaves the chicken alone with the grain, the
 * chicken will eat the grain. How can the farmer bring everything
 * to the far side of the river intact?
 *
 * authors: Greg Dennis, Rob Seater
 */</i><b>

module models/examples/tutorial/farmer
open util/ordering[State]

</b><i>/*
 * The farmer and all his possessions will be represented as Objects.
 * Some objects eat other objects when the Farmer's not around.
 */</i><b>
abstract sig Object { eats: set Object }
one sig Farmer, Fox, Chicken, Grain extends Object {}

</b><i>/*
 * Define what eats what when the Farmer' not around.
 * Fox eats the chicken and the chicken eats the grain.
 */</i><b>
fact eating { eats = Fox->Chicken + Chicken->Grain}

</b><i>/*
 * The near and far relations contain the objects held on each
 * side of the river in a given state, respectively.
 */</i><b>
sig State {
   near: set Object,
   far: set Object
}

</b><i>/*
 * In the initial state, all objects are on the near side.
 */</i><b>
fact initialState {
   let s0 = first() |
     s0.near = Object && no s0.far
}

</b><i>/*
 * Constrains at most one item to move from 'from' to 'to'.
 * Also constrains which objects get eaten.
 */</i><b>
pred crossRiver (from, from', to, to': set Object) {
  </b><i>// either the Farmer takes no items</i><b>
  ( from' = from - Farmer && 
    to' = to - to.eats + Farmer ) ||
  </b><i>// or the Farmer takes one item</i><b>
  some item: from - Farmer {
    from' = from - Farmer - item
    to' = to - to.eats + Farmer + item
  }
}

</b><i>/*
 * crossRiver transitions between states
 */</i><b>
fact stateTransition {
  all s: State, s': next(s) {
    Farmer in s.near =>
      crossRiver(s.near, s'.near, s.far, s'.far),
      crossRiver(s.far, s'.far, s.near, s'.near)
  }
}

</b><i>/*
 * the farmer moves everything to the far side of the river.
 */</i><b>
pred solvePuzzle () {
     last().far = Object
}
run solvePuzzle for 8 State expect 1
</pre></b>

<hr WIDTH="100%">

<p> This is the only version of this mode.

<hr WIDTH="100%">

<p> <a href="alloy-tutorial.html" target="_top">restart </a> the
tutorial from the beginning.

  </body>
</html>
