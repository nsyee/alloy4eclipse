<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Alloy Tutorial Model</title>
    <link   rel=stylesheet   href="style-currentmodel.css"   type="text/css">
  </head>

  <body>
    <h2>Ord module</h2>

<p> <a href="currentmodel-Ord-simple.html" target="currentmodel"> hide
    comments</a>

<pre>
<b>module util/ordering[elem]

</b><i>/*
 * Creates a single linear ordering over the atoms in elem. It also constrains all
 * the atoms to exist that are permitted by the scope on elem. That is, if the scope
 * on a signature S is 5, opening util/ordering[S] will force S to have 5 elements
 * and create a linear ordering over those five elements. The predicates and
 * functions below provide access to properties of the linear ordering, such as
 * which element is first in the ordering, or whether a given element precedes
 * another. You cannotcreate multiple linear orderings over the same signature with
 * this model. If you that functionality, try using the util/sequence module instead.
 *
 * Technical comment:
 * An important constraint: elem must contain all atoms permitted by the scope.
 * This is to let the analyzer optimize the analysis by setting all fields of each
 * instantiation of Ord to predefined values: e.g. by setting 'last' to the highest
 * atom of elem and by setting 'next' to {<T0,T1>,<T1,T2>,...<Tn-1,Tn>}, where n is
 * the scope of elem. Without this constraint, it might not be true that Ord.last is
 * a subset of elem, and that the domain and range of Ord.next lie inside elem.
 *
 * author: Ilya Shlyakhter
 * revisions: Daniel jackson
 */</i><b>

one sig Ord {
   first_, last_: elem,
   next_, prev_: elem -> lone elem
}{
</b><i>  // constraints that actually define the total order
</i><b>  prev_ = ~next_
  one first_
  one last_
  no first_.prev_
  no last_.next_
  (
</b><i>   // either elem has exactly one atom,
   // which has no predecessor or successor...
</i><b>   (one elem && no elem.prev_ && no elem.next_) ||
</b><i>   // or...
</i><b>    all e: elem | {
</b><i>      // ...each element (except the first) has one predecessor, and...
</i><b>      (e = first_ || one e.prev_)
</b><i>      // ...each element (except the last) has one successor, and...
</i><b>      (e = last_ || one e.next_)
</b><i>      // ...there are no cycles
</i><b>      (e !in e.^next_)
    }
  )
</b><i>  // all elements of elem are totally ordered
</i><b>  elem in first_.*next_
}

</b><i>// first and last
</i><b>fun first (): elem { Ord.first_ }
fun last  (): elem { Ord.last_ }

</b><i>// return the predecessor of e, or empty set if e is the first element
</i><b>fun prev (e: elem): lone elem { e.(Ord.prev_) }
</b><i>// return the successor of e, or empty set of e is the last element
</i><b>fun next (e: elem): lone elem { e.(Ord.next_) }

</b><i>// return elements prior to e in the ordering
</i><b>fun prevs (e: elem): set elem { e.^(Ord.prev_) }
</b><i>// return elements following e in the ordering
</i><b>fun nexts (e: elem): set elem { e.^(Ord.next_) }

</b><i>// e1 is less than e2 in the ordering
</i><b>pred lt (e1, e2: elem) { e1 in prevs (e2) }
</b><i>// e1 is greater than e2 in the ordering
</i><b>pred gt (e1, e2: elem) { e1 in nexts (e2) }
</b><i>// e1 is less than or equal to e2 in the ordering
</i><b>pred lte (e1, e2: elem) { e1=e2 || lt (e1,e2) }
</b><i>// e1 is greater than or equal to e2 in the ordering
</i><b>pred gte (e1, e2: elem) { e1=e2 || gt (e1,e2) }

</b><i>// returns the larger of the two elements in the ordering
</i><b>fun larger (e1, e2: elem): elem { if lt (e1,e2) then e2 else e1 }
</b><i>// returns the smaller of the two elements in the ordering
</i><b>fun smaller (e1, e2: elem): elem { if lt (e1,e2) then e1 else e2 }

</b><i>// returns the largest element in es
// or the empty set if es is empty
</i><b>fun max (es: set elem): lone elem { es - es.^(Ord.prev_) }

</b><i>// returns the smallest element in es
// or the empty set if es is empty
</i><b>fun min (es: set elem): lone elem { es - es.^(Ord.next_) }
</b></pre>

<hr WIDTH="100%">

<p> <a href="currentmodel-Ord-simple.html" target="currentmodel"> hide
    comments</a>

<hr WIDTH="100%">

<p> <a href="alloy-tutorial.html" target="_top">restart </a> the
tutorial from the beginning.

  </body>
</html>
