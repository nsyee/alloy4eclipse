grammar fr.univartois.cril.alloyplugin.Basics with org.eclipse.xtext.common.Terminals 
hidden (WS, ML_COMMENT, SL_COMMENT)
generate basics "http://www.univartois.fr/cril/alloyplugin/Basics"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/*
	specification ::= [module] open* paragraph*
*/
Specification  :
  module=Module? open+=Open* paragraph+=Paragraph*; 

/*
	module ::= "module" name  [ "["  ["exactly"] name  ("," ["exactly"] num)*    "]" ]
*/
Module:
  "module" moduleName=Name ("[" "exactly"? suite=Name (Comma "exactly"? INT)* "]")?; 
  
/*
	open ::= ["private"]  "open"  name  [ "[" ref,+ "]" ]  [ "as" name ]
*/
Open:
  Private? "open" name=Name ("[" ref=Ref (Comma ref2+=Ref)* "]")? ("as" name2=Name)?; 
  
/*
	paragraph ::= factDecl | assertDecl | funDecl | cmdDecl | enumDecl | sigDecl
*/
Paragraph:
  FactDecl		|
  AssertDecl	|
  FunDecl		|
  PredDecl		|
  RunDecl		|
  CheckDecl		|
  EnumDecl		|
  SigDecl	; 

/*
	factDecl ::= "fact" [name] block
*/
FactDecl returns Fact:
  "fact" factName=TypeName? b=Block; 

/*
	assertDecl ::= "assert" [name] block
*/
AssertDecl returns Assertion:
  "assert" assertName=AssertionName? b=Block; 
  
/*
	funDecl ::= ["private"] "fun" [ref "."] name "(" decl,* ")" ":" expr block
	funDecl ::= ["private"] "fun" [ref "."] name "[" decl,* "]" ":" expr block
	funDecl ::= ["private"] "fun" [ref "."] name                ":" expr block
*/
FunDecl returns Function:
  Private? "fun" (ref=Ref Dot)? funName=TypeName ("[" (decl=Decl (Comma decl2+=Decl)*)? "]"|
  "(" (decl=Decl (Comma decl2+=Decl)*)? ")") Colon expr=Expression b=Block; 
  
/*
	funDecl ::= ["private"] "pred" [ref "."] name "(" decl,* ")" block
	funDecl ::= ["private"] "pred" [ref "."] name "[" decl,* "]" block
	funDecl ::= ["private"] "pred" [ref "."] name                block
*/
PredDecl returns Predicate:
  Private? pred="pred" (ref=Ref Dot)? predName=PredicateName ("[" (decl=Decl (Comma decl2+=Decl)
  *)? "]"|"(" (decl=Decl (Comma decl2+=Decl)*)? ")")? b=Block; 
  
/*
	cmdDecl ::= [name ":"] ["run"|"check"] [name|block] scope //JUST RUN
*/
RunDecl returns RunCommand:
  (runName=Alias ":")? run="run" (name2=[PredicateName]|b=Block) scope=Scope; 
  
/*
	cmdDecl ::= [name ":"] ["run"|"check"] [name|block] scope //JUST CHECK
*/
CheckDecl returns CheckCommand:
  (checkName=Alias ":")? check="check" (name2=[AssertionName]|b=Block) scope=Scope; 
  
/*
	scope ::= "for" number                   ["expect" [0|1]]
	scope ::= "for" number "but" typescope,+ ["expect" [0|1]]
	scope ::= "for"              typescope,+ ["expect" [0|1]]
	scope ::=                                ["expect" [0|1]]	
*/
Scope:
	"for" INT 														   ("expect" ("0"|"1"))?	|
	"for" INT "but" typescope+=Typescope ("," typescope+=Typescope)*   ("expect" ("0"|"1"))?	|
	"for"           typescope+=Typescope ("," typescope+=Typescope)*   ("expect" ("0"|"1"))?	|
																	   ("expect" ("0"|"1"))?	;	
  //("for" (INT ("but" typescope=Typescope (Comma typescope=Typescope)*)?|typescope=Typescope (Comma typescope=Typescope)*)?)? ("expect" ("0"|"1"))?; 
  
/*
	typescope ::= ["exactly"] number [name|"int"|"seq"]
*/
Typescope:
  "exactly"? INT (n=[TypeName]|("int"|"seq")); 
  
/*
	enumDecl ::= "enum" name "{" name  ("," name)*  "}"
*/
EnumDecl:
  "enum" enumName=TypeName LeftCurlyBracket name2=TypeName (Comma name3+=TypeName)* RightCurlyBracket; 
  
/*
	sigDecl ::= sigQual* "sig" name,+ [sigExt] "{" decl,* "}" [block]
*/
SigDecl returns Signature:
  SigQual? "sig" sigName+=TypeName (Comma sigName+=TypeName)* (sigExt=SigExt)? LeftCurlyBracket (decl+=Decl ("," decl+=Decl)*)? RightCurlyBracket (b=Block)?; 
  
/*
	sigQual ::= "abstract" | "lone" | "one" | "some" | "private"	
*/
SigQual:
  sigq=( "abstract" | "lone" | "one" | "some" | "private" ) ("abstract"|"lone"|"one"|"some"|"private")*; 
  
/*
	sigExt ::= "extends" ref
	sigExt ::= "in" ref ["+" ref]*
*/
SigExt:
  "extends" ref=Ref|"in" ref=Ref ("+" ref2+=Ref)*; /*
	expr ::= "let" letDecl,+ blockOrBar						1./	
       | quant decl,+    blockOrBar							2./
       | unOp expr											3./
       | expr binOp   expr									4./
       | expr arrowOp expr									5./
       | expr ["!"|"not"]? compareOp expr					6./
       | expr ["=>"|"implies"] expr "else" expr 			7./
       | expr "[" expr,* "]"								8./
       |     number											9./
       | "-" number											10./
       | "none"												11./
       | "iden"												12./
       | "univ"												13./
       | "Int"												14./
       | "seq/Int"											15./
       | "(" expr ")"										16./
       | ["@"] Name											17./
       | block												18./
       | "{" decl,+ blockOrBar "}"							19./
	
*/
Expression:
	TerminalExpression (b=BinOp expr=Expression									|//4./
						a=ArrowOp expr=Expression								|//5./
						("!"|"not")? cmp=CompareOp expr=Expression				|//6./
						("=>"|"implies") expr=Expression "else" expr=Expression	|//7./
						"[" (expr=Expression ("," expr=Expression)* )? "]"									 //8./						
						)*;
TerminalExpression returns Expression:
	"let" letdecl+=LetDecl ("," letdecl+=LetDecl)* blockOrBar=BlockOrBar	|//1./
	Quant decl+=Decl ("," decl+=Decl)* blockOrBar=BlockOrBar				|//2./
	unOp=UnOp expr=Expression												|//3./
	INT																		|//9./
	Integer																	|//10./
	"none"																	|//11./
    "iden"																	|//12./
    "univ"																	|//13./
    "Int"																	|//14./
    "seq/Int"																|//15./
    "(" Expression ")"														|//16./
    "@"? nameRef=Name														|//17./
    Block																	|//18./
    "{" decl+=Decl ("," decl+=Decl)* blockOrBar=BlockOrBar "}"				;//19./

/*
	decl ::= ["private"] ["disj"] name,+ ":" ["disj"] expr
*/
Decl:
  (Private)? ("disj")? nameExpression+=PropertyName (Comma nameExpression+=PropertyName)* Colon ("disj")? exprDecl=Expression; 
  
/*
	letDecl ::= name "=" expr
*/
LetDecl:
  nameExpression=PropertyName "=" exprLet=Expression; 
  
/*
	arrowOp ::= ["some"|"one"|"lone"|"set"]? "->" ["some"|"one"|"lone"|"set"]? //redondance in the end of the expression
*/
Quant	:
	"all" | "no" | "some" | "lone" | "one" | "sum";
	
BinOp	: 
	"||" | "or" | "&&" | "and" | "&" | "<=>" | "iff"
        | "=>" | "implies" | "+" | "-" | "++" | "<:" | ":>" | "." | "<<" | ">>" | ">>>";
UnOp	:
	"!" | "not" | "no" | "some" | "lone" | "one" | "set" | "seq" | "#" | "~" | "*" | "^";
	
CompareOp	:
	"=" | "in" | "<" | ">" | "=<" | "<=" /* UNDOCUMENTED */| ">=";
	
ArrowOp:
  ("some"|"one"|"lone"|"set")? s="->"; 
   
/*
	block ::= "{" expr* "}"
*/
Block:
  LeftCurlyBracket (exp+=Expression)* RightCurlyBracket; 

/*
	blockOrBar ::= block
	blockOrBar ::= "|" expr
*/
BlockOrBar:
  Block				|
  "|" Expression	; 
  
/*
	name ::= ["this" | ID] ["/" ID]*
*/
Name:
	this="this"|name=ID|nameComplete=NameBis;

Alias:
	name=ID|nameComplete=NameBis;
	  
AssertionName :
	this="this"|name=ID|nameComplete=NameBis;

PredicateName :
	this="this"|name=ID|nameComplete=NameBis;
	
TypeName:
	this="this"|name=ID|nameComplete=NameBis;
	
PropertyName:
	this="this"|name=ID|nameComplete=NameBis;

NameBis:
  mainName=("this"|ID) ("/" suite+=ID)+; 
  
/*
	ref ::= name | "univ" | "Int" | "seq/Int"
*/
Ref:
  name=[TypeName]|"univ"|"Int"|"seq/Int";

LeftCurlyBracket:
  "{";

RightCurlyBracket:
  "}";

Private:
  "private";
  
Comma :
",";
Dot :
".";
Colon :
":";
terminal Integer:
  "-" INT;
terminal ML_COMMENT	:
	('/*'->'*/' ) | ('--'->'\n') ;

terminal ID:
  ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')* ("'")*;
